### **新版AS的gradle和旧版本的gradle的区别**

#### **1. 新版本的AS创建android项目的时候下`app`的`build.gradle`中没有**
```xml
allproject{
     repositories {}
}
```
是因为这个转移到了`setting.gradle`配置下
```xml
pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}
```

#### **2. 新版的AS创建的Android项目下的模块的`build.gradle`中没有`buildToolVersion`**
```xml
android { 
    //编译版本
    compileSdk 31

    //buildToolVersion "31.0.2"编译工具版本，新版本的AS中不用显式写出，工具会自动调用

    //默认配置
    defaultConfig {
        //app唯一标识
        applicationId "com.bo.cloudmusic"

        //最小版本
        minSdk 21

        //目标版本
        targetSdk 31

        //版本号
        versionCode 1

        //版本名称
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }
}
```
使用新的android gradle插件3.x,您不再需要为构建工具指定版本(因此,您现在可以删除该android.buildToolsVersion属性).
默认情况下,插件会自动使用您正在使用的Android插件版本所需的最低构建工具版本.


#### **3. 手动导入模块下所有的libs和jar**
```xml
implementation fileTree(dir: 'libs',includes: ['*.jar'])
```

#### **4. jcenter()库和mavenCenter()库**
[参考郭霖大大的文章](https://guolin.blog.csdn.net/article/details/119706565?spm=1001.2101.3001.6650.1&utm_medium=distribute.pc_relevant.none-task-blog-2%7Edefault%7ECTRLIST%7ERate-1.pc_relevant_antiscanv2&depth_1-utm_source=distribute.pc_relevant.none-task-blog-2%7Edefault%7ECTRLIST%7ERate-1.pc_relevant_antiscanv2&utm_relevant_index=2)



#### **5. 新版的AS中`AllProject`不在`build.gradle(:app)`中,里面的东西放在`setting.gradle`中**
```java
pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()

        maven {url "https://jitpack.io"}
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()

        maven {url "https://jitpack.io"}
    }
}
rootProject.name = "随波云音乐"
include ':app'
```

#### **6. 新版AS中的项目的build.gradle中默认没有buildscript，可以自己加**
```java
build.gradle:

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        // 1.添加MobSDK Maven地址
        maven { url "https://mvn.mob.com/android"}
    }
    dependencies {
        // 2.注册MobSDK
        classpath "com.mob.sdk:MobSDK:2018.0319.1724"
    }
}

plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
}

apply from: "config.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
```